package BabyAnimal
import BonyAnimal
import Orders
import GeometryUtils
import ClosureTimers
// Using initlater fixes a bug that causes unintended deaths of units.
// I don't know why but think it's something related to data corruption.
import initlater Pets

let SPAWN_WANDER_DISTANCE = 100.

public abstract class BabyAnimal extends BonyAnimal
    construct(unit whichUnit)
        super(whichUnit)

    override function postCreate()
        super.postCreate()
        let baby = getUnit()
            ..issuePointOrderById(Orders.move, getPos().polarOffset(randomAngle(), SPAWN_WANDER_DISTANCE))
            ..addAbility(GHOST_VIS_ID)
        doAfter(1) ->
            if baby.isAlive()
                baby.issueImmediateOrderById(Orders.stop)

    override function getNumCorpses() returns int
        if pets.has(getUnit().getOwner())
            return 0
        return super.getNumCorpses()
